

path /users/{uid} {
  read() { isCurrentUser(uid) }
  write() { isCurrentUser(uid) }
}


path /locations/{approxLat} {
  read() { isSignedIn() }
}
path /locations/{approxLat}/{approxLong} {
  read() { isSignedIn() }
  write() { isSignedIn() }
}
path /locations/{approxLat}/{approxLong}/{gameId} is GameInfo {
  read() { isSignedIn() && areLocationsClose(getMyLocation(), this.location) && !isExpired(this) }
  write() { isSignedIn() }
}


type GameInfo {
  location: Location,
  type: String,
  id: String,
  timestamp: InitialTimestamp,
}

type UUID extends String {
  validate() { this.length == 36 }
}

type Location {
  lat: Number,
  long: Number,
}

type CurrentTimestamp extends Number {
  validate() { this == now }
}

type InitialTimestamp extends Number {
  validate() { initial(this, now) }
}

isSignedIn() { auth != null }
isCurrentUser(uid) { auth != null && auth.uid == uid }

initial(value, init) { value == (prior(value) == null ? init : prior(value)) }
writeOnce(value) { prior(value) == null ? true : value == prior(value) }

isExpired(data) { now - data.timestamp > 60 * 1000 }
getMyLocation() { root.users[auth.uid].location }

distanceThreshold() { 0.0002 }
areLocationsClose(loc1, loc2) {
  loc1.lat - loc2.lat < distanceThreshold() &&
  loc2.lat - loc1.lat < distanceThreshold() &&
  loc1.long - loc2.long < distanceThreshold() &&
  loc2.long - loc1.long < distanceThreshold()
}
